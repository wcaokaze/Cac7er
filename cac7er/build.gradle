
final VERSION = '0.8.0'
version VERSION

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.dokka_version = '0.10.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.6'
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$projectDir/../docs/cac7er"

    configuration {
        moduleName = 'Cac7er'

        includes = []
        jdkVersion = 6

        skipEmptyPackages = true
        reportUndocumented = false

        sourceLink {
            path = "$projectDir/src/main/java"
            url = 'http://github.com/wcaokaze/Cac7er/blob/master/cac7er/src/main/java'
            lineSuffix = '#L'
        }

        externalDocumentationLink {
            url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from dokka.outputDirectory
}

javadocJar.dependsOn(dokka)

final pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'http://opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'wcaokaze'
            name 'wcaokaze'
            email 'wcaokaze@gmail.com'
        }
    }

    scm {
        url 'https://github.com/wcaokaze/Cac7er.git'
        connection 'scm:git:https://github.com/wcaokaze/Cac7er.git'
        developerConnection 'scm:git:https://github.com/wcaokaze/Cac7er.git'
    }
}

publishing {
    publications {
        Cac7er(MavenPublication) {
            groupId 'com.wcaokaze'
            artifactId 'cac7er'
            version VERSION

            artifact sourcesJar
            artifact javadocJar
            artifact "$buildDir/libs/Cac7er-${VERSION}.jar"

            pom.withXml {
                final rootNode = asNode()

                final dependenciesNode = rootNode['dependencies'][0]
                        ?: rootNode.appendNode('dependencies')

                rootNode.appendNode('name', 'Cac7er')

                for (dependency in configurations.implementation.allDependencies) {
                    if (dependency.name == 'unspecified') { continue }

                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId',    dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version',    dependency.version)
                }

                rootNode.children().last() + pomConfig
            }
        }
    }
}

publish.dependsOn(sourcesJar, javadocJar, jar)

bintray {
    final props = new Properties()
    props.load(project.file('local.properties').newDataInputStream())

    user = props.getProperty('bintray.user')
    key = props.getProperty('bintray.apiKey')

    publications = ['Cac7er']

    pkg {
        repo = 'maven'
        name = 'Cac7er'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/wcaokaze/Cac7er'

        version {
            name = VERSION
            released = new Date()
            vcsTag = "v$VERSION"
        }
    }
}

bintrayUpload.dependsOn(publish)
