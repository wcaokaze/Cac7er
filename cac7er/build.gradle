
version '0.7.0'

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.dokka_version = '0.10.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.6'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.6'

    kotlinOptions {
        freeCompilerArgs = [
                '-Xuse-experimental=kotlin.Experimental',
        ]
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = 'docs'

    configuration {
        moduleName = 'Cac7er'

        includes = ['src/main/java/packages.md']
        jdkVersion = 6

        skipEmptyPackages = true
        reportUndocumented = false

        sourceLink {
            path = "$projectDir/src/main/java"
            url = 'http://github.com/wcaokaze/Cac7er/blob/master/cac7er/src/main/java'
            lineSuffix = '#L'
        }

        externalDocumentationLink {
            url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

def localRepo = new File(rootDir, 'repository')

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${localRepo.absolutePath}"
            pom.version = version
            pom.groupId = 'com.wcaokaze'
            pom.artifactId = 'cac7er'
        }
    }
}

jar.baseName = 'cac7er'
