
version '0.7.0'

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.dokka_version = '0.9.17'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'

    api "com.wcaokaze:cac7er:$version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("com.wcaokaze:cac7er:$version") with project(':Cac7er')
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.6'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.6'

    kotlinOptions {
        freeCompilerArgs = [
                '-Xuse-experimental=kotlin.Experimental',
        ]
    }
}

dokka {
    moduleName = 'Cac7er-Vue'
    outputFormat = 'html'
    outputDirectory = 'docs'

    includes = ['src/main/java/packages.md']
    jdkVersion = 6

    skipEmptyPackages = true
    reportUndocumented = false

    linkMapping {
        dir = "$projectDir/src/main/java"
        url = 'http://2wiqua.wcaokaze.com/gitbucket/wcaokaze/Cac7er/blob/master/cac7er-vue/src/main/java'
        suffix = '#L'
    }

    externalDocumentationLink {
        url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
    }
}
